#
# Exposes information about the Python environment via the $python_info
# associative array.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# function python-info {

local virtualenv_name
local virtualenv_format
local virtualenv_formatted

# Clean up previous $python_info.
unset python_info
typeset -gA python_info

# Format virtualenv.
if [[ -n "$VIRTUAL_ENV" ]]; then
  if [[ -n "${VIRTUALENV_DIR_NAMES[(r)${VIRTUAL_ENV:t}]}" ]]; then
    virtualenv_name=${${VIRTUAL_ENV:h}:t}
  else
    virtualenv_name=${VIRTUAL_ENV:t}
  fi

  zstyle -s ':prezto:module:python:info:virtualenv' format 'virtualenv_format'
  zformat -f virtualenv_formatted "$virtualenv_format" "v:$virtualenv_name"
  python_info[virtualenv]="$virtualenv_formatted"
fi

# }
